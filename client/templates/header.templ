

@html = ->
  div '#header', ->
    # Insert content you want to show up on every page here
    a '.connect_status .fb     .offline', href:'#', 'Facebook'
    a '.connect_status .google .offline', href:'#', 'Google'
    span '.connect_status', "API's"
    a '#docs_link', href:'/docs', 'Documentation'

@init = ($el)->
  update_api_ui = ->
    $el.find('.online') .removeClass 'online'
    $el.find('.offline').removeClass 'offline'
    for app in ['fb', 'google']
      status = if Houce.apis.is_connected app then 'online' else 'offline'
      $el.find('.'+app).addClass status
  update_api_ui()
  
  $el.find('.connect_status').click ->
    app = if $(@).hasClass 'fb' then 'fb' else 'google'
    if $(@).hasClass 'offline'
      Houce.oauth2.connect app
      update_api_ui()
    else
      Houce.oauth2.disconnect app
      location.reload()
    return false


@style =
  i_header:
    padding: '20px 21.5% 0 20%'
    position: 'relative'
    zIndex: 1
    c_connect_status:
      float:  'right'
      margin: '0 4px'
    i_docs_link:
      float:  'right'
      marginRight: '20px'
      color: 'blue'

  c_offline: color: 'red'
  c_online:  color: 'green'
